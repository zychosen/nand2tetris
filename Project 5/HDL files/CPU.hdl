// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    // Read instructions, data and load relevant registers
    Mux16(a=instruction, b=ALUf, sel=instruction[15], out=toAReg);
    Not(in=instruction[15], out=not15);
    Or(a=not15, b=instruction[5], out=loadA);

    ARegister(in=toAReg, load=loadA, out=outREG, out[0..14]=addressM);

    And(a=instruction[4], b=instruction[15], out=toD);

    DRegister(in=ALUf, load=toD, out=toALU);
    Mux16(a=outREG, b=inM, sel=instruction[12], out=toALU2);
    
    // Write operation
    And(a=instruction[3], b=instruction[15], out=writeM);

    // ALU operation
    ALU(x=toALU, y=toALU2, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, zr=zeroOUT, ng=negOUT, out=ALUf);

    // Control unit
    Not(in=zeroOUT, out=comp);
    Not(in=negOUT, out=comp1);

    And(a=comp, b=comp1, out=x1);
    Or(a=zeroOUT, b=comp1, out=x2);
    Or(a=negOUT, b=zeroOUT, out=x3);

    Mux(a=false, b=x1, sel=instruction[0], out=out1);
    Mux(a=zeroOUT, b=x2, sel=instruction[0], out=out2);
    Mux(a=negOUT, b=comp, sel=instruction[0], out=out3);
    Mux(a=x3, b=true, sel=instruction[0], out=out4);
    Mux(a=out1, b=out2, sel=instruction[1], out=out5);
    Mux(a=out3, b=out4, sel=instruction[1], out=out6);
    Mux(a=out5, b=out6, sel=instruction[2], out=l);

    And(a=l, b=instruction[15], out=loadPC);
    Not(in=loadPC, out=incPC);
    PC(in=outREG, load=loadPC, inc=incPC, reset=reset, out[0..14]=pc);
}